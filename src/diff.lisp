;;;; diff.lisp

(in-package #:cliki2)

(defun format-revisions-diff (origin modified)
  (let ((diff:*diff-context-lines* 2))
    (write-to-string
     (diff:generate-diff 'wiki-diff
                         origin
                         modified))))

(defclass wiki-diff (diff:diff) ()
  (:default-initargs
   :window-class 'wiki-diff-window))

(defclass wiki-diff-window (diff:diff-window) ())

(defmethod print-object :before ((window wiki-diff-window) stream)
  (write-line (cliki2.view:diff-line-number
               (list :origin-start (diff:original-start-line window)
                     :modified-start (diff:modified-start-line window)))
              stream))

(defmethod print-object ((window wiki-diff-window) stream)
  (iter (for origin in (select-origin-chunks (diff:window-chunks window)))
        (for modified in (select-modified-chunks (diff:window-chunks window)))
        (cond
          ((string= origin modified)
           (write-string (cliki2.view:diff-common-line
                          (list :line origin))
                         stream))
          
          ((and origin modified)
           (write-line (cliki2.view:diff-line
                        (simple-compare-strings (closure-template:escape-html origin)
                                         (closure-template:escape-html modified)))
                       stream))
                          
          (t (write-string (cliki2.view:diff-line
                            (list :origin (closure-template:escape-html origin)
                                  :modified (closure-template:escape-html modified)))
                           stream)))))

(defun select-origin-chunks (chunks)
  (iter (for chunk in chunks)
        (case (diff:chunk-kind chunk)
          ((:common :delete)
           (dolist (line (diff:chunk-lines chunk))
             (collect line)))
          (:replace
           (collect (format nil "窿ㄤ殒婧汨躅氕扉铄汨躅氅┅ê泸遽翦ㄤ镬轶扉铄ㄤ殒婧汨躅氕扉铄汨躅氅ㄤ邈灬蝈ㄩ珙矧扉铄┅ㄣ镬戾泗铋飑┅┅ㄤ彐躅箦戾泗盹溟骈邃汨躅塍ㄣ桴铍螬ㄩ翦ㄦ矧汨躅轭汨躅塍ㄣ狍ㄤ殒婧汨躅氕腴钿汨躅氅è恒镯盹恒蝈狒濠ㄤ镬轶扉铄ㄤ殒婧汨躅氕扉铄汨躅氅ㄣ镬戾泗扉铄┅ê轭箦螋ㄣ镬戾泗ㄦ矧磲铋窿ㄤ殒婧汨躅氕扉铄汨躅氅┅ê溴戾翦ㄤ镬轶扉铄ㄤ殒婧汨躅氕扉铄汨躅氅ㄤ邈灬蝈ㄩ珙矧扉铄┅ㄣ镬戾泗铋飑┅┅ㄤ彐躅箝眇戾泔眇狎瀛篝蜷铉矧殓轭盹溟骈邃灬忮祗è篝虿狎篝颟磲箝眇戾鲥泗矧＇汨狎泔溴篝颟黩篝秕脲篝狎孱洎ㄩ翦ㄦ矧骝镯篝狎忮祜孱洎ㄦ矧汨ㄣ栳篝椹ㄩㄣ栳蚪汨＼五黛轭濠黩轸瀛扉铄⒓怛劲秕舂黩轸瀛汨狎汨秕舂┅ㄦ眙箦篝镦骟弭骢瞟鏖翳秕麴豸麸篝蜷铉秕舂ㄩ翦ㄦ矧箢犭轭箦螬ㄦ矧痱弼箢犭痱弼轱躞箢犭濠ㄦ矧沲颦箢犭瀛篝狎ㄦ躅汜祆镦骟弭骢箢犭濠ㄦ矧沲颦箢犭瀛戾铉翳ㄤ殒婧箢犭瀛戾铉翳箢犭濠ㄦ矧痱弼箢犭瀛孱ㄩ痱弼箢犭ǐㄦ躅汜祆镦骟弭骢痱弼箢犭濠ㄤ殒婧箢犭瀛戾铉翳痱弼箢犭濠癌躅戾篌沲颦箢犭瀛篝狎痱弼箢犭瀛孱洎黩轸瀛篝蜷铉⒓箴犷劲秕舂黩篝秕后翎螋痱弼箢犭瀛孱哄钿沲颦箢犭瀛篝狎舂黩轸瀛篝蜷铉⒓箴犷劲秕舂麒孱沲颦箢犭瀛戾铉翳癌黩篝秕后翎螋沲颦箢犭瀛篝狎哄钿ǐ沲颦箢犭瀛篝狎沲颦箢犭瀛戾铉翳┅┅┅戾è箦ㄤ殒婧泔眇豸瀛煦篝虿狎矧殓轭篝虿狎盹溟骈邃┅┅扉篝猴蜷玳ㄦ眙箦矧殓轭＇溟骀猴蜷玳钺飙镦骟弭喉镤殒殄ㄦ眙箦盹溟骈邃＇溟骀喉镤殒殄洵镦骟弭┅┅